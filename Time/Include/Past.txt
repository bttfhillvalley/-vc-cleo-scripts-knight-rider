{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE variables.txt}

:CheckPastTravel
0AB4: 3@ = var TRAVEL
if
    3@ == 1
then
    0AB4: 3@ = var SET_TIME
    if
        3@ == 1
    then
        repeat
            wait 0
            0AB4: 3@ = var SET_TIME
        until 3@ == 0
    end
    0AB4: 3@ = var CDATE
    0AB4: 4@ = var CTIME

    if
        001E:   1@ > 3@  // Greater Date
    then
        Car.Destroy(vehicle)
        cleo_return 1 true
    end
    if and
        003C:   1@ == 3@ // Same Date
        001E:   2@ > 4@  // Greater Time
    then
        Car.Destroy(vehicle)
        cleo_return 1 true
    end

end
cleo_return 1 false

:Past
while true
    wait 0
    0AB4: 2@ = var CDATE
    0AB4: 3@ = var CTIME
    0AB4: 4@ = var SET_TIME

    // Handle when we are time travelling
    if
        4@ == 1 // Setting time right now
    then
        repeat
            wait 0
            0AB4: 4@ = var SET_TIME
        until 4@ == 0
        0AB4: 2@ = var CDATE
        0AB4: 3@ = var CTIME
        repeat
            wait 0
            0AB4: 4@ = var CFADE
        until 4@ == 0
        if and
            003C:   2@ == 0@ // Same Date
            003C:   3@ == 1@ // Same Time
        then
            0AB4: 5@ = var MODE
            if
                5@ == 0 // Cutscene mode
            then
                cleo_return 1 NORMAL_REENTRY  // Do normal reentry
            else
                cleo_return 1 INST_REENTRY // Do Instant reentry
            end
        end
        // Check same date, but time is in the future
        if and
            003C:   2@ == 0@ // Same Date
            001E:   3@ > 1@  // Greater Time
        then
            cleo_return 1 PRELOAD_REENTRY // flag to spawn time machine immediately
        else if
            001E:   2@ > 0@ // Greater Date
        then
            cleo_return 1 PRELOAD_REENTRY // flag to spawn time machine immediately
        end
        end
    end

    // Normal passage of time, wait for the time to come
    if and
        003C:   2@ == 0@ // Same Date
        003C:   3@ == 1@ // Same Time
    then
        // Play Doc's beeper
        3F84: play_sound "doc_watch_beep.wav" loop 0
        wait 750
        cleo_return 1 NORMAL_REENTRY  // Do normal reentry
    end
end