{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE Variables.txt}

:EntryExplosion
// Entry is normal yellow explosion
0B10: 0@ = 0@ AND JVT_BIT // Time Circuits Fried
4@ = 43
if
    0@ == 0 // Delorean
then
    5@ = 5.0
else
    5@ = 10.0
end
jump @CreateExplosion

:ReentryExplosion
0B10: 0@ = 0@ AND JVT_BIT // Time Circuits Fried
if
    0@ == 0 // Delorean
then
    4@ = 45  // Blue
    5@ = 5.0
else
    4@ = 43  // Yellow
    5@ = 10.0
end

:CreateExplosion
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
0437: scatter_particle 4@ 5@ at 1@ 2@ 3@ 0 0 0
cleo_return 0

:ReentrySonicBoom
// 3 sonic booms, or one if its instant
0B10: 0@ = 0@ AND JVT_BIT // Time Circuits Fried
cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
0AB4: 6@ = var MODE
0AB4: 7@ = var TRAVEL
if or
    6@ == 0 // Cutscene mode?
    7@ == 0 // Not time travelling?
then
    // Current Time
    0AB4: 8@ = var CDATE
    0AB5: 9@ = var CTIME
    008D: 8@ = integer_to_float 8@
    008D: 9@ = integer_to_float 9@
    9@ /= 2400.0
    005B: 8@ += 9@

    // Last Time
    008D: 4@ = integer_to_float 4@
    008D: 5@ = integer_to_float 5@
    5@ /= 2400.0
    005B: 4@ += 5@

    // Calculate time delta
    0063: 4@ -= 8@
    if
        4@ < 0.0
    then
        4@ *= -1.0
    end
    4@ /= 10000.0

    if
        0@ == 0 // Delorean
    then
        if
            4@ < 1.0
        then
            3F95: 15@ = play_sound "delorean_short_reentry.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            wait 200
            cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
            wait 300
            cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
        else
            0087: 10@ = 4@
            0AEF: 4@ = log 4@ base 2.718281828459045 //all floats
            4@ *= 88.0
            4@ += 400.0
            0087: 5@ = 4@
            5@ *= 1.05
            008C: 4@ = float_to_integer 4@
            008C: 5@ = float_to_integer 5@
            if
                10@ > 50.0
            then
                3F95: 15@ = play_sound "delorean_long_reentry.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            else
                3F95: 15@ = play_sound "delorean_reentry_single.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            end
            // Two more explosions if we're in cutscene
            wait 4@
            if
                10@ > 50.0
            then
                3F95: 15@ = play_sound "delorean_long_reentry.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            else
                3F95: 15@ = play_sound "delorean_reentry_single.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            end
            cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
            wait 5@
            cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
            if
                10@ > 50.0
            then
                3F95: 15@ = play_sound "delorean_long_reentry.wav" pos 1@ 2@ 3@ loop 0 min 50.0
            end

        end
        3F95: 15@ = play_sound "delorean_reentry_3.wav" pos 1@ 2@ 3@ loop 0 min 50.0
    else
        3F95: 15@ = play_sound "train_reentry.wav" pos 1@ 2@ 3@ loop 0 min 50.0
        wait 750
        cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
        wait 700
        cleo_call @ReentryExplosion 4 0@ 1@ 2@ 3@
    end
end
cleo_return 0