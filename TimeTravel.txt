{$CLEO .s}
{$INCLUDE Common/CleoConstants.txt}
{$INCLUDE Time/Include/Variables.txt}
0000:
const
    KEY_HOVERCONVERSION = 67 // C
    KEY_DEBUG = 190 // "."
end
thread 'TIME'
gosub @Init
cleo_call @Setup 6 vehicle vehicle_flags dest_date dest_time past_date past_time

while true
    wait 10
    if
        Car.Wrecked(vehicle)
    then
        Car.RemoveReferences(vehicle)
        if
            entry_date == -1
        then
            terminate_this_custom_script
        end
        gosub @Reentry

    end
    // Handle local and global variables
    gosub @MemoryHandling

    // Debug
    cleo_call @DebugVariation 2 vehicle vehicle_flags ret vehicle_flags

    // Reset stuff
    cleo_call @ResetVariation 2 vehicle vehicle_flags
    cleo_call @ResetCamera 2 vehicle vehicle_flags

    // Animations
    cleo_call @FluxCapacitorAnimation 3 vehicle vehicle_flags timera ret timera
    cleo_call @FlashTimeCircuits 2 vehicle vehicle_flags

    // Delorean stuff
    cleo_call @Fuel 2 vehicle vehicle_flags ret vehicle_flags
    cleo_call @Hook 2 vehicle vehicle_flags ret vehicle_flags
    cleo_call @HoverConversion 2 vehicle vehicle_flags ret vehicle_flags
    cleo_call @Fly 2 vehicle vehicle_flags

    // Check for any Time Travelling
    cleo_call @CheckPastTravel 3 vehicle entry_date entry_time ret result
    if
        result == true
    then
        gosub @Reentry
        continue
    end

    // Struck by lightning?
    cleo_call @CheckLightning 2 vehicle vehicle_flags ret result

    // Lightning check
    if
        result == true
    then
        0B11: vehicle_flags = vehicle_flags OR FC_FRIED_BIT
        0B11: vehicle_flags = vehicle_flags OR TC_FRIED_BIT
        0B11: vehicle_flags = vehicle_flags OR LIGHTNING_BIT
        gosub @TimeTravel
        continue
    end

    // Clocktower Run
    /*cleo_call @CheckWire 2 vehicle vehicle_flags ret result
    if
        result == true
    then
        gosub @SetLastDeparted
        cleo_call @Entry 2 vehicle vehicle_flags ret vehicle_flags x y z rx ry rz velocity
        cleo_call @SetTime 4 vehicle vehicle_flags dest_date dest_time
        gosub @Reentry
    end*/

    // Time Travel Effects
    cleo_call @CheckSpeed 2 vehicle vehicle_flags ret result
    if
        result == true
    then
        gosub @TimeTravel
    end
end

:TimeTravel
gosub @SetLastDeparted
cleo_call @Entry 2 vehicle vehicle_flags ret vehicle_flags x y z rx ry rz velocity
cleo_call @SetTime 4 vehicle vehicle_flags dest_date dest_time
gosub @Reentry
return

// Include all the helper threads here
{$INCLUDE Common/DrawTexture.txt}
{$INCLUDE Time/Include/Camera.txt}
{$INCLUDE Time/Include/Coils.txt}
{$INCLUDE Time/Include/Debug.txt}
{$INCLUDE Time/Include/Effects.txt}
{$INCLUDE Time/Include/Entry.txt}
{$INCLUDE Time/Include/Explosion.txt}
{$INCLUDE Time/Include/FluxCapacitor.txt}
{$INCLUDE Time/Include/Fuel.txt}
{$INCLUDE Time/Include/Hook.txt}
{$INCLUDE Time/Include/Hover.txt}
{$INCLUDE Time/Include/Init.txt}
{$INCLUDE Time/Include/Lightning.txt}
{$INCLUDE Time/Include/Memory.txt}
{$INCLUDE Time/Include/Model.txt}
{$INCLUDE Time/Include/Past.txt}
{$INCLUDE Time/Include/Plasma.txt}
{$INCLUDE Time/Include/Plate.txt}
{$INCLUDE Time/Include/Reentry.txt}
{$INCLUDE Time/Include/SetTime.txt}
{$INCLUDE Time/Include/Sound.txt}
{$INCLUDE Time/Include/SpeedOffset.txt}
{$INCLUDE Time/Include/TimeCircuits.txt}
{$INCLUDE Time/Include/Wire.txt}
{$INCLUDE Time/Include/Wormhole.txt}