{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
0000:
thread 'WEATHER'
int date
int day
int hour
int minute
int curr_hour
int curr_minute
int set_time_var
float x
float y
float z
int actor
int vehicle
int player_health
00BF: hour = current_time_hours, minute = current_time_minutes
while true
    wait 0
    // Get Date
    0AB4: date = var CDATE
    00BF: curr_hour = current_time_hours, curr_minute = current_time_minutes

    if
        not curr_hour == hour
    then
        hour = curr_hour
        if
            date == 19551112
        then
            if
                hour == 20
            then
                01B5: force_weather 2
            end

            if
                hour == 23
            then
                01B5: force_weather 0
            end
        end
    end

    if
        not curr_minute == minute
    then
        minute = curr_minute
        if and
            date == 20151027
            hour == 16
            minute == 34
        then
            set_weather 0
        end
    end

    // Nth dimension.  Only if we have an impossible day (Jan 00)
    day = date
    date /= 100
    date *= 100
    day -= date
    if
        day == 0
    then
        00C0: set_current_time 23 0
        set_weather 20

        // Slow health drain
        if
            80DE:   not player $PLAYER_CHAR driving_vehicle_type 237
        then
            if
                timera > 3000
            then

                0225: player_health = player $PLAYER_CHAR health
                0332: set_actor $PLAYER_ACTOR bleeding_to 1
                if
                    player_health > 0
                then
                    player_health -= 1
                    0222: set_player $PLAYER_CHAR health_to player_health
                    timera = 0
                end
            end
        else
            timera = 0
            0332: set_actor $PLAYER_ACTOR bleeding_to 0
        end


        // Peds with guns and chaos.  Pistols so the Delorean doesn't get destroyed immediately.
        00A0: store_actor $PLAYER_ACTOR position_to x y z
        if
            0AE1: actor = random_char_near_point x y 10.0 in_radius 500.0 find_next 0 pass_deads 1
        then
            repeat
                if
                    8248:   not model #COLT45 available
                then
                    0247: request_model #COLT45
                    038B: load_requested_models
                end
                if
                    803A: not actor == $PLAYER_ACTOR
                then
                    01B2: give_actor actor weapon 17 ammo 100 // Load the weapon model before using this
                    01B9: set_actor actor armed_weapon_to 17
                    011A: set_actor actor search_threat 0xFFFFFF
                end
            until 8AE1: not actor = random_char_near_point x y z in_radius 500.0 find_next 1 pass_deads 1
            0249: release_model #COLT45
        end
    end
end
